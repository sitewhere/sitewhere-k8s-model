plugins {
    id 'org.springframework.boot' version '2.4.4' apply false
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'java'
}

apply plugin: 'distribution'
    
allprojects {
    group = 'com.sitewhere'
    version = '3.1.0'
	
    repositories {
        mavenLocal()
        mavenCentral()
    }
	apply plugin: 'eclipse'
	
	apply plugin: "com.github.hierynomus.license"
	license {
		header rootProject.file('HEADER')
    	include "**/*.java"
	}
 	
 	// Choose Java settings.
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    
	// Library dependencies.
    dependencies {
        // Sping dependency managment.
        implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
           	
    	// Fabric8 Kuberentes client.
		compile group: 'io.fabric8', name: 'kubernetes-client', version: '4.13.0'
	
		// Used for helper functions.
		implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
		
        testCompile group: 'junit', name: 'junit'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

// Publish jar.	
apply plugin: 'maven-publish'
publishing {
	publications {
		mavenJava(MavenPublication) {
		    from components.java
            artifact sourcesJar
            artifact javadocJar
			pom {
			    name = 'SiteWhere Kubernetes Model'
			    description = 'SiteWhere Kubernetes Entity Model'
			    url = 'http://sitewhere.io'
			    licenses {
			        license {
			            name = 'CPAL-1.0'
			            url = 'https://opensource.org/licenses/CPAL-1.0'
			        }
			    }
                developers {
                    developer {
                        id = 'admin'
                        name = 'SiteWhere Admin'
                        email = 'admin@sitewhere.com'
                    }
                }
                scm {
                    connection = 'https://github.com//sitewhere/sitewhere-k8s-model.git'
                    url = 'https://github.com//sitewhere/sitewhere-k8s-model'
                }
			}	
		}
	}
    repositories {
        maven {
            url project.nexusUrl
            credentials {
                username project.nexusUsername
                password project.nexusPassword
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
	apply plugin: 'signing'
	signing {
	    sign publishing.publications.mavenJava
	}
}
